# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion webserver feature parse_multipart_form_data
#
# -----------------------------------------------------------------------

# extract mime values from a given part of formdata
#
# NYI: CLEANUP: this code should be cleanup up further
#
extract_mime_values(part String) =>
  part_lines := part.split "\r\n" .as_array_backed
  mv := mime_values

  for
    part_lines_header_count := 0, part_lines_header_count + 1
    part_line in part_lines
  while part_line.find ":" .exists
  do
    mv.add part_line
  else
    for
      res := "", res + new_val
      i in part_lines_header_count+1..part_lines.count-1
    do
      new_val := (res.is_empty ? "" : "\n") + part_lines[i]
    else
      (mv, res)


# extract form data
#
module parse_multipart_form_data(data, boundary String) lock_free.Map String String =>

  lock_free.Map String String .from_entries (
    data
      .split "--{boundary}"
      .filter (l -> !l.starts_with "--") # filter out end boundary
      .map (.trim)
      .flat_map part->
        mv, value_str := extract_mime_values part
        cd := mv.get "Content-Disposition"
        name := cd["name"]
        if name != "" then [(name, value_str)] else [])

