# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion webserver feature parse_multipart_form_data
#
# -----------------------------------------------------------------------

# NYI: current complexity is O(n^2). this should be reduced.
module parse_multipart_form_data(data, boundary String) lock_free.Map String String =>

  # Convert boundary to Fuzion-friendly format
  bndry := "--" + boundary
  end_bndry := bndry + "--"
  # Initialize form data map
  form_data := (lock_free.Map String String).empty
  lines := data
    .split "\r\n"
    .as_array
  i := mut 0
  while i < lines.count do
    line := lines[i].trim
    if line = bndry
      # Start of a new part
      v := mime_values
      i <- i + 1
      if i < lines.count

        # Read headers
        while i < lines.count && (lines[i].find ":").exists do
          l := lines[i].trim
          if l != ""
            v.add l
          i <- i+1
        cd := v.get "Content-Disposition"

        if i < lines.count
          value_str :=
            for res := "", res + new_val
            while i < lines.count && lines[i] != bndry && lines[i] != end_bndry
            do
              new_val := (res.is_empty ? "" : "") + lines[i]
              i <- i + 1
          name := cd["name"]
          if name != ""
            form_data.put name value_str

    else if line = end_bndry
      # End of multipart data
      i <- lines.count
    else
      i <- i + 1
  form_data
