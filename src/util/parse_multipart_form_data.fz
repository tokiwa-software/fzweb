# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion webserver feature parse_multipart_form_data
#
# -----------------------------------------------------------------------

# NYI: current complexity is O(n^2). this should be reduced.
module parse_multipart_form_data(data, boundary String) lock_free.Map String String =>

  # Convert boundary to Fuzion-friendly format
  bndry := "--" + boundary
  # Initialize form data map
  form_data := (lock_free.Map String String).empty
  data
    .split bndry
    .filter (l -> !l.starts_with "--") # filter out end boundary
    .map (.trim)
    .for_each (part ->
      part_lines := part.split "\r\n" .as_array
      v := mime_values

      part_lines_header_count :=
        for
          ixxx := 0, ixxx + 1
          part_line in part_lines
        while part_line.find ":" .exists
        do
          v.add part_line
        else
          ixxx

      value_str :=
        for
          res := "", res + new_val
          i in part_lines_header_count+1..part_lines.count-1
        do
          new_val := (res.is_empty ? "" : "\n") + part_lines[i]
        else
          res

      cd := v.get "Content-Disposition"
      name := cd["name"]
      if name != ""
        form_data.put name value_str
    )
  form_data
