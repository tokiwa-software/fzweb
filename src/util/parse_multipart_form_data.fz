# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion webserver feature parse_multipart_form_data
#
# -----------------------------------------------------------------------

# NYI: current complexity is O(n^2). this should be reduced.
module parse_multipart_form_data(data, boundaryy String) lock_free.Map String String =>

  # Convert boundary to Fuzion-friendly format
  boundary := "--" + boundaryy
  end_boundary := boundary + "--"
  # Initialize form data map
  form_data := (lock_free.Map String String).empty
  lines := data
    .split "\r\n"
    .as_array
  i := mut 0
  while i < lines.count do
    line := lines[i].trim
    if line = boundary
      # Start of a new part
      v := mime_values
      i <- i + 1
      if i < lines.count

        # Read headers
        while i < lines.count && (lines[i].find ":").exists do
          l := lines[i].trim
          if l != ""
            v.add l
          i <- i+1
        cd := v.get "Content-Disposition"

        if i < lines.count
          valueStr :=
            for res := "", res + new_val
            while i < lines.count && lines[i] != boundary && lines[i] != end_boundary
            do
              new_val := (res.is_empty ? "" : "") + lines[i]
              i <- i + 1
          name := cd.get_parameter "name"
          if name != ""
            form_data.put name valueStr

    else if line = end_boundary
      # End of multipart data
      i <- lines.count
    else
      i <- i + 1
  form_data
