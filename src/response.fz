# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion webserver feature response
#
# -----------------------------------------------------------------------

# response -- represents an http response from the webserver
#
module response (returncode i32,
                 attributes lock_free.Map String String,
                 data Sequence u8,
                 module session option Session) is

  return_code_string =>
    if returncode = 200
      "OK"
    else if returncode = 302
      "Found"
    else if returncode = 403
      "Forbidden"
    else if returncode = 404
      "Not Found"
    else if returncode = 415
      "Unsupported Media Type"
    else if returncode = 500
      "Internal Server Error"
    else if returncode = 501
      "Not Implemented"
    else
      "NYI"


  # get the bytes of this response
  #
  module bytes(LM type : mutate) =>
    attributes.put "Server" "Fuzion WebServer v0.0.2"
    if !attributes["Connection"].exists
      attributes.put "Connection" "close"
    br := io.Read_Handler.from LM data.as_array
    http.response_message
        .new nil 1 1 returncode return_code_string attributes.items.as_array br
        .bytes data.count


  # short-hand for creating a response with an empty session field
  #
  module type.new(returncode i32,
                  attributes lock_free.Map String String,
                  data Sequence u8) response =>
    response returncode attributes data nil


  # short-hand for creating a response with an empty session field and no data
  #
  module type.new(returncode i32,
                  attributes lock_free.Map String String) response =>
    response returncode attributes [] nil


  # short-hand for creating a response with an empty session field,
  # no data and no attributes
  #
  module type.new(returncode i32) response =>
    response returncode (lock_free.Map String String).empty [] nil
