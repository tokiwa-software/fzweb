# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion webserver feature logger
#
# -----------------------------------------------------------------------

module logger is

  # Atomic integer to maintain a unique thread ID
  thread_id_count := concur.atomic i32 .new 0

  # Thread local storage for thread IDs
  thread_id :=
    for x := thread_id_count.read
    while !(thread_id_count.compare_and_set x x+1)
    else x+1

  # Logger function that outputs to the console
  module log(c String|error) =>
    say "{time.now.get} {logger.thread_id}: {c ? s String => s | e error => e.as_string}"

  # Logger function that writes to a file
  module log(log_path path, msg String) =>
    m := "{time.now.get} {logger.thread_id}: {msg}"

    match (io.file.use unit log_path io.file.mode.append ()->
            (io.file.writer.write "{m}\n".utf8))
      unit => say m # Also log to console
      e error => say "*** failed writing to log: {e}"
