# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion webserver feature logger
#
# -----------------------------------------------------------------------

module logger is

  # Atomic integer to maintain a unique thread ID
  thread_id_count := concur.atomic 0

  # Thread local storage for thread IDs
  thread_id :=
    for x := thread_id_count.read
    while !(thread_id_count.compare_and_set x x+1)
    else x+1

  # Logger function that outputs to the console
  module log(s2 String) =>
    say "{logger.thread_id}: {s2}"

  # Logger function that writes to a file
  module log(log_path String, msg String) =>
    res := io.file.use unit log_path io.file.mode.append ()->
        m := "{logger.thread_id}: {msg}"
        (io.file.writer.write "{m}\n".utf8).error

    match res
      unit => say "{logger.thread_id}: {msg}"  # Also log to console
      e error => say "*** failed to write to log: {e}"
