# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion webserver feature connection_handler
#
# -----------------------------------------------------------------------

request_method : choice of
  GET is
    redef as_string => "GET"
  POST is
    redef as_string => "POST"
  UNKNOWN is
    redef as_string => "UNKNOWN"


# to be passed to server.accept
# handle_connection is the feature that
# is used to handle requests
#
module connection_handler : net.Connection_Handler unit is

  # contains convenient access of request
  # like header fields, a send feature etc.
  #
  request_context (LM type : mutate) is
    read_header String =>
      header_lines := io.buffered.read_line_while LM (s -> !s.is_empty)
      String.from_codepoints (header_lines.as_codepoints.drop_while (x -> x = "\n"))

    http_header_map(s String) container.Map String String =>
      res := s
        .split "\n"
        .map (x -> (x.find " ").map_to_option (idx -> (x.substring 0 idx, x.substring idx+1 x.byte_length)))
        .filter (x -> x.exists)
        .map (x -> x.get)
        .as_array
      (container.ps_map String String).new (res.map x->x.values.0) (res.map x->x.values.1)

    head(method request_method, url String, http_headers container.Map String String) is
      path => (url.split "?").first
      query_string => ((url.split "?").drop 1).first ""

    _head := once LM head ()->
      s := read_header
      method request_method := if s.starts_with "GET" then GET else if s.starts_with "POST" then POST else UNKNOWN
      url := String.from_codepoints (s
        .as_codepoints
        .drop_while (x -> x != " ")
        .drop_while (x -> x = " ")
        .take_while (x -> x != " "))
      line2ff := (s.as_codepoints.drop_while (x ->  x != "\n")).drop 1
      line2ff0 := String.from_codepoints line2ff
      map := http_header_map line2ff0
      head method url map

    head => _head.get

    client =>
      match net.channel.env.get_peer_address
        x list u8 => x
        e error => [u8 0, 0, 0, 0].as_list

    send(return_code i32, attributes String, data array u8) is
      header := """
        HTTP/1.1 $return_code OK
        Connection: close
        Server: Fuzion WebServer v0.0.2
        {attributes}
      """
      header_ok := (io.buffered.writer LM).write header.utf8.as_array

      match header_ok.error
        e error => say "#### {header_ok.error.err}"
        unit =>
          data_ok := (io.buffered.writer LM).write data
          if data_ok.error!!
            say "#### {data_ok.error.err}"
          flush_ok := (io.buffered.writer LM).flush
          if flush_ok!!
            say "#### {flush_ok.err}"


  # entry point to the handling the request
  #
  redef handle_connection (LM type : mutate) unit =>
    req_context := request_context LM
    say "{req_context.head.method} {req_context.head.url}"

    # calls to java may sometimes trigger panic
    panic
      .try unit ()->
        process_request LM req_context
      .catch m->
        say "handling request, got fault: {m}"


  # helper to process the request
  #
  process_request(LM type : mutate, req_context request_context LM) =>
    # compute response
    get_response(cmd String) =>
      post_len =>
        match (req_context.head.http_headers["Content-Length:"].or_else "0").parse_i32
          e error => 0
          i i32 => i
      post_data := String.from_bytes (io.buffered.read_bytes LM post_len).as_list
      val := (jflang.webserver_S_Value_static.new (req_context.head.http_headers["Content-Type:"].or_else ""))
      content_type := val.getType
      content_type_boundary := val.getBoundary
      form_data := get_form_data post_data cmd req_context.head.query_string content_type content_type_boundary

      match form_data
        x (lock_free.map String String) =>
          cl Java.java.net.InetAddress := (jnet.InetAddress_static.getByAddress (req_context.client.map y->y.cast_to_i8)).val
          route_request req_context.head.path cmd cl x req_context.head.query_string
        error =>
          jflang.webserver_S_Response_static.new 415 "Accept: application/x-www-form-urlencode, multipart/form-data\n" (list i8).empty

    resp := match req_context.head.method
      GET => get_response "GET"
      POST => get_response "POST"
      UNKNOWN => jflang.webserver_S_Response_static.new 500 "" (list i8).empty

    _ := req_context.send resp.getReturncode resp.getAttributes (resp.getData.as_array.map_to_array x->x.as_u8)
