# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion webserver feature runner
#
# -----------------------------------------------------------------------

module runner is

  # maximum bytes to read from process
  #
  max_output_bytes => 1E5

  # maximum time to wait for process to finish
  #
  max_wait_time    => time.duration.s 30

  # compile and run some code
  #
  run_code(
    # the Session used for sending back results
    s Session,
    # the actual code
    code String,
    # the id of where the results belong in the html
    results_to String,
    # true execute, false analyze for effects
    normal bool) =>

    # NYI read_lines, send intermediate results
    args :=
      if normal
        ["-oLogLevel=FATAL", "flang@jenkins.tokiwa.software", "timeout", "--preserve-status", "--signal=15",
         "--kill-after=22s", "20s", "bwrap", "--ro-bind", "/", "/", "--dev-bind", "/dev", "/dev", "--unshare-all",
         "--clearenv", "--setenv", "PATH", "/run/current-system/sw/bin:/etc/profiles/per-user/flang/bin",
         "fuzion/build/bin/fz", "-"]
      else
        ["-oLogLevel=FATAL", "flang@jenkins.tokiwa.software", "timeout", "--preserve-status", "--signal=15",
         "--kill-after=22s", "20s", "bwrap", "--ro-bind", "/", "/", "--dev-bind", "/dev", "/dev", "--unshare-all",
         "--clearenv", "--setenv", "PATH", "/run/current-system/sw/bin:/etc/profiles/per-user/flang/bin",
         "fuzion/build/bin/fz", "-effects", "-"]

    s.send_event "pushElementContents" results_to "starting job ..."

    r := os.process.start "ssh" args
      .bind p->

        # write code to stdin
        _ := p.write_string code

        x := p.wait max_wait_time
         .bind ec->
           lm : mutate is
           if ec = 0
             lm ! ()->
               p.with_out _ lm ()->
                 String.from ((io.buffered lm).read_bytes max_output_bytes .val)
           else
             lm ! ()->
               p.with_err _ lm ()->
                 String.from ((io.buffered lm).read_bytes max_output_bytes .val)

        _ := p.send_signal os.signals.kill

        x

    s.send_event "pushElementContents" results_to r.as_string


  # helper feature
  #
  module run(s Session, form_data lock_free.Map String String) unit =>
    run_code s form_data["code"].val form_data["resultsTo"].val true

  # helper feature
  #
  module runeff(s Session, form_data lock_free.Map String String) unit =>
    run_code s form_data["code"].val form_data["resultsTo"].val false
