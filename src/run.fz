# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion webserver feature run
#
# -----------------------------------------------------------------------

# run -- feature that starts up the Fuzion webserver
#
run =>

  port u16 := 8080
  # start the server
  res := net.server.start net.family.ipv4 net.protocol.tcp port ()->
    logger.log "started listening on port: $port"

    # global instance of webserver
    #
    webserver := webserver (lock_free.Map String Session).empty Runner Feeds

    webserver.feeds.init


    # start a thread pool
    # NYI: UNDER DEVELOPMENT: thread_pool size should be determined programmatically
    concur.thread_pool 4 ()->

      # the request accept loop
      do
        accept_res := net.server.env.accept.bind unit conn->

          lm : mutate is

          _ := conn.in_thread_pool unit concur.thread_pool lm lm ()->
            fuzion.runtime.fault
              .try ()->
                webserver.handle_connection lm conn

                # NYI: ugly, since (io.buffered lm).writer is instated
                # outside of lambda `fuzion.runtime.fault.try ()->`
                # we need to flush to make sure deinstating writer effect later
                # does not crash the whole webserver.
                #
                match (io.buffered lm).writer.env.flush
                  e error => logger.log "failed flushing, connection meanwhile closed?\n\n {e}"
                  unit =>

              .catch  e->
                logger.log "fault: $e"
                match conn.close
                  cce error => logger.log "failed closing connection {cce}"
                  unit    =>

        accept_res.or_else ()->
          logger.log "failed accepting connection {accept_res}"

  logger.log "some failure occurred: $res"
