# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion webserver feature content
#
# -----------------------------------------------------------------------

# content -- features for working with the content stored on the file system
#
module content is

  # Base directory corresponding to simple_path.
  #
  # This base directory will contain the pages subdirectory
  # and the access.txt file.
  #
  type.base_dir (simple_path String) Java.java.nio.file.Path =>
    bp := base_simple_path simple_path
    bp?? ? content_dir.resolve_Ljava_7_nio_7_file_7_Path_s_ bp.get : content_dir


  # Base directory corresponding to simple_path, or nil
  # if it could not be found.
  #
  # This base directory will contain the pages subdirectory
  # and the access.txt file.
  #
  module type.base_simple_path (simple_path String) option Java.java.nio.file.Path =>
    sp := jpath_of simple_path

    base_simple_path_loop_helper (i i32) option Java.java.nio.file.Path =>
      if i > 0
        base_simple_path := sp.subpath 0 i
        base_dir0 := content_dir.resolve_Ljava_7_nio_7_file_7_Path_s_ base_simple_path
        access := base_dir0.resolve "access.txt"

        if Java.java.nio.file.Files_static.exists access (list Java.java.nio.file.LinkOption).empty
          base_simple_path
        else
          base_simple_path_loop_helper i-1
      else
        nil

    base_simple_path_loop_helper sp.getNameCount


  # The file part of a simple_path.
  #
  module type.file_path (simple_path String) Java.java.nio.file.Path =>
    bp := base_simple_path simple_path
    sp := jpath_of simple_path
    start i32 := bp!! ? 0 : bp.get.getNameCount
    end i32 := sp.getNameCount
    start < end ? sp.subpath start end : jpath_of ""


  # look the given attribute in the access.txt file
  # for simple_path
  #
  type.access_attr (simple_path, attribute, default String) String =>
    access_file := (base_dir simple_path).resolve "access.txt"
    c := config_file.open access_file.toString
    a := c.get attribute
    a.get default


  # Check if access to a given path is permitted for user u.
  #
  module type.access_permitted (u option user, simple_path String) bool =>
    groups := access_attr simple_path "groups" ""
    match u
      u0 user => u0.check_group_access groups
      nil =>
        for
          s in (groups.split " ")
        until s = "all"
          true
        else
          false


  # Check if the given path points to a directory that contains an index.html
  # file that contains a <browse> tag to browse a directory.
  #
  type.is_browsable (jndex Java.java.nio.file.Path) option (lock_free.Map String String) =>
    if Java.java.nio.file.Files_static.exists jndex (list Java.java.nio.file.LinkOption).empty
      util
        .read_file_as_string jndex.toString
        .bind str->
          for
            l in str.split "\n"
            m := Session.get_tag l "<browse"
          until m.exists
            outcome m.get
          else
            error "not found"
        .as_option
    else
      nil


  # pages dir for a given path prefix
  #
  type.pages_dir (simple_path String) Java.java.nio.file.Path =>
    (base_dir simple_path).resolve_Ljava_7_nio_7_file_7_Path_s_ pages_dir_relative


  # index file for a given directory containing pages
  #
  type.index_file (pages Java.java.nio.file.Path) Java.java.nio.file.Path =>
    pages.resolve "index.html"


  # Get the relative directory from a <browse> tag. This is either
  # the value of the attribute "dir" or, if attribute "userdata" is
  # "true", the user directory.
  #
  type.get_dir_from_browse (u option user, data_path Java.java.nio.file.Path, map lock_free.Map String String) option String =>
    if "true" = (map["userdata"].get "false")
      u.bind String u0->
        ud := jpath_of u0.base_dir
        ru := data_path.getParent.relativize ud
        ru.toString
    else
      map["dir"]


  # check if a file is known to be preformatted
  #
  module type.is_known_preformatted_file (file_name String) bool =>
    file_name.ends_with ".c" ||
      file_name.ends_with ".fz" ||
      file_name.ends_with ".fuzion" ||
      file_name.ends_with ".sh" ||
      file_name.ends_with ".txt" ||
      file_name.ends_with ".ebnf"


  # Get the data path to the contents of the given simple URL path after it had
  # been checked that we have permission to access it.
  #
  type.get_data_path2 (u option user, simple_path String) Java.java.nio.file.Path =>
    pages := pages_dir simple_path
    html_file := mut simple_path

    if !(html_file.get.ends_with ".html") && !(is_known_preformatted_file html_file.get)
      html_file <- html_file.get + ".html"

    r := pages.resolve_Ljava_7_nio_7_file_7_Path_s_ (file_path html_file.get)
    if !(Java.java.nio.file.Files_static.exists r (list Java.java.nio.file.LinkOption).empty)
      index_file pages
      # NYI browsing
    else
      r


  type.path_from_access (simple_path, attribute, default String) Java.java.nio.file.Path =>
    val := access_attr simple_path attribute default
    bd := base_dir simple_path
    bd.resolve val


  # Get the data path to the contents of the given simple URL path and check if
  # the given user (or nil if none) is allowed to access it.
  #
  module type.get_data_path (u option user, simple_path String) Java.java.nio.file.Path =>
    if access_permitted u simple_path
      get_data_path2 u simple_path
    else
      if u!!
        path_from_access simple_path "alternative.need.login" "need_login.html"
      else
        path_from_access simple_path "alternative.access.denied" "access_denied.html"


  # Determine the index.html file located in simple_path's base dir.
  #
  type.index_path (simple_path String) Java.java.nio.file.Path =>
    index_file (pages_dir simple_path)


  # From a URL path, find the corresponding content identifier, or FORBIDDEN /
  # MISSING in case this does not exist.
  #
  module type.get_contents (s Session, path_from_url0 String) choice identifier missing forbidden =>
    res := mut (choice identifier missing forbidden) forbidden
    path_from_url := mut path_from_url0

    if path_from_url.get.starts_with "/"
      path_from_url <- path_from_url.get.substring 1
      p := jpath_of path_from_url.get

      if !p.isAbsolute && (p.equals p.normalize) && (content_dir.resolve_Ljava_7_nio_7_file_7_Path_s_ p).normalize.startsWith_Ljava_7_nio_7_file_7_Path_s_ content_dir
        if path_from_url.get.ends_with ".html"
          path_from_url <- path_from_url.get.substring 0 (path_from_url.get.find_last ".html").get

        match template path_from_url.get
          missing =>
            dir := base_dir path_from_url.get
            access_file := dir.resolve "access.txt"
            access := config_file.open access_file.toString
            content_path := dir.resolve_Ljava_7_nio_7_file_7_Path_s_ (file_path path_from_url.get)

            if path_from_url.get.ends_with ".png" || path_from_url.get.ends_with ".svg" || path_from_url.get.ends_with ".jpg" || path_from_url.get.ends_with ".jpeg" || path_from_url.get.ends_with ".pdf"
              if access_permitted s.get_user path_from_url.get
                res <- identifier.new content_path path_from_url.get
            else
              res <- missing
              match access.get "template"
                nil => s.log "no template defined in {access_file}"
                t String =>
                  res <- identifier (dir.resolve t) path_from_url.get content_path
          * => res <- template path_from_url.get

    res.get


  # helper to find a template's identifier
  #
  module type.template (path_from_url String) choice identifier missing forbidden =>
    p := templates_dir.resolve path_from_url
    if (Java.java.nio.file.Files_static.exists p (list Java.java.nio.file.LinkOption).empty) && !(Java.java.nio.file.Files_static.isDirectory p (list Java.java.nio.file.LinkOption).empty)
      identifier.new p nil
    else
      missing


  # constants
  #
  type.content_dir Java.java.nio.file.Path => (jpath_of "./content").normalize
  type.pages_dir_relative Java.java.nio.file.Path => jpath_of "pages"
  module type.templates_dir Java.java.nio.file.Path => (jpath_of "./templates").normalize


# identifies a page alongside a template to send to the user
#
module identifier (module file_to_send option Java.java.nio.file.Path,
                   module simple_path option String,
                   content_path option Java.java.nio.file.Path) is

  is_html_template := content_path.exists


  module type.new (file_to_send option Java.java.nio.file.Path,
                   simple_path option String) identifier =>
    identifier file_to_send simple_path nil


  module get_bytes (s Session) outcome (array u8) =>
    match file_to_send
      fts Java.java.nio.file.Path =>
        data := io.file.use fts.toString io.file.mode.read ()->
          (io.buffered io.file.file_mutate).read_fully

        match data
          ba array u8 =>
            if is_html_template
              s.set_current (simple_path.get "")
              s.add_session_info_to_html_bytes ba
            else
              ba
          e error => e
      nil => error "no file to send"


# indicates a page that is missing
#
module missing is


# indicates a page that the user is not allowed to access
#
module forbidden is


# short-hand to invoke java.nio.file.Path.of
# with a single string argument
#
jpath_of (path String) Java.java.nio.file.Path =>
  Java.java.nio.file.Path_static.of path []
