# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion webserver feature document
#
# -----------------------------------------------------------------------

# document -- features used to work with documents served by the webserver
#
module document is

  # takes a page filename and removes the HTML suffix, or returns
  # index if the filename is empty
  #
  module strip_html(page String) String =>
    if page.ends_with ".html" || page.ends_with ".htm"
      page.substring 0 (page.find ".").get
    else if page = ""
      "index"
    else
      page


  # added the html extension to the page filename
  #
  module add_html(page String) String =>
    if !content.is_known_preformatted_file page
      page + ".html"
    else
      page


  # read lines from the path and return them in a list of string
  # NYI: pretty printing
  #
  module read_all_lines (file Java.java.nio.file.Path,
                         return_nil_on_error, pretty_print bool)
                        option (list String) =>
    res := io.file.use (list String) file.toString io.file.mode.read ()->
      data := (io.buffered io.file.file_mutate).read_fully
      lines := String.from_bytes data
      lines.split "\n"

    match res
      e error =>
        logger.log "error reading from path {file.toString}: {e}"
        if return_nil_on_error
          nil
        else
          ["--error reading file--"].as_list
      re list String => re


  # same as read_all_lines (3 args) but without the option
  # for pretty printing
  #
  read_all_lines (file Java.java.nio.file.Path,
                  return_nil_on_error bool)
                 option (list String) =>
    read_all_lines file return_nil_on_error false


  # same as read_all_lines (2 args) but without the option of
  # returning nil on error
  #
  module read_all_lines (file Java.java.nio.file.Path) list String =>
    (read_all_lines file false).get


  # given a `link` and a path `page_file`, attempt to extract the title of
  # the page at `page_file`.
  #
  # this is done by searching for the first `<h1>` tag and extracting the
  # title between the opening tag and the closing tag.
  #
  # if this fails, return the file name as given by `link`, without an `html`
  # suffix, as the title.
  #
  module get_title (link String, page_file Java.java.nio.file.Path) String =>
    title_start := "<h1>"
    title_end := "</h1>"
    pattern_h1_start_tag := Java.java.util.regex.Pattern_static.compile "<h1.*?>"

    for
      line in document.read_all_lines page_file
      x := (pattern_h1_start_tag.matcher (fuzion.jvm.env.cast Java.java.lang.CharSequence line.as_java_object).get).replaceFirst "<h1>"
      start := (x.find title_start).get -1
      end := start >= 0 ? (x.find title_end (start + title_start.byte_length)).get -1 : -1
    until start >= 0 && end >= 0
      x.substring (start + title_start.byte_length) end
    else
      strip_html (content.file_path link).toString
